
model {

#####LIKELIHOOD
for(i in 1:n){
  Ann.ln[i,First[i]] ~ dnorm(mu.Ann.ln[year[i]],tau.Ann.ln[year[i]])
  Joh.ln[i,First[i]] ~ dnorm(mu.Joh.ln[year[i]],tau.Joh.ln[year[i]])
  Mer.ln[i,First[i]] ~ dnorm(mu.Mer.ln[year[i]],tau.Mer.ln[year[i]])
  AnnJoh.ln[i,First[i]] ~ dnorm(mu.AnnJoh.ln[year[i]],tau.AnnJoh.ln[year[i]])
  AnnMer.ln[i,First[i]] ~ dnorm(mu.AnnMer.ln[year[i]],tau.AnnMer.ln[year[i]])
  JohMer.ln[i,First[i]] ~ dnorm(mu.JohMer.ln[year[i]],tau.JohMer.ln[year[i]])
  
    
  for(t in First[i]:(Last[i]-1)){
    enc.hist[i,(t+1)] ~ dbern(S[i,t]*enc.hist[i,t])   
    logit(S[i,t]) <- beta.P[pair[i]] + beta.Y[year[i]] + beta.S.attempt*attempt[i] + w.a*b.Ann.ln*Ann.ln[i,(t+1)] + w.b*b.Joh.ln*Joh.ln[i,(t+1)] + w.c*b.Mer.ln*Mer.ln[i,(t+1)] + w.d*b.AnnJoh.ln*AnnJoh.ln[i,(t+1)] + w.e*b.AnnMer.ln*AnnMer.ln[i,(t+1)] + w.f*b.JohMer.ln*JohMer.ln[i,(t+1)]    

    Ann.ln[i,(t+1)] ~ dnorm(mn.Ann.ln[i,(t+1)],tau.Ann.ln[year[i]])
    mn.Ann.ln[i,(t+1)] <- mu.Ann.ln[year[i]] + rho.Ann.ln[year[i]]*(Ann.ln[i,t]-mu.Ann.ln[year[i]])

    Joh.ln[i,(t+1)] ~ dnorm(mn.Joh.ln[i,(t+1)],tau.Joh.ln[year[i]])
    mn.Joh.ln[i,(t+1)] <- mu.Joh.ln[year[i]] + rho.Joh.ln[year[i]]*(Joh.ln[i,t]-mu.Joh.ln[year[i]])

    Mer.ln[i,(t+1)] ~ dnorm(mn.Mer.ln[i,(t+1)],tau.Mer.ln[year[i]])
    mn.Mer.ln[i,(t+1)] <- mu.Mer.ln[year[i]] + rho.Mer.ln[year[i]]*(Mer.ln[i,t]-mu.Mer.ln[year[i]])

    AnnJoh.ln[i,(t+1)] ~ dnorm(mn.AnnJoh.ln[i,(t+1)],tau.AnnJoh.ln[year[i]])
    mn.AnnJoh.ln[i,(t+1)] <- mu.AnnJoh.ln[year[i]] + rho.AnnJoh.ln[year[i]]*(AnnJoh.ln[i,t]-mu.AnnJoh.ln[year[i]])

    AnnMer.ln[i,(t+1)] ~ dnorm(mn.AnnMer.ln[i,(t+1)],tau.AnnMer.ln[year[i]])
    mn.AnnMer.ln[i,(t+1)] <- mu.AnnMer.ln[year[i]] + rho.AnnMer.ln[year[i]]*(AnnMer.ln[i,t]-mu.AnnMer.ln[year[i]])

    JohMer.ln[i,(t+1)] ~ dnorm(mn.JohMer.ln[i,(t+1)],tau.JohMer.ln[year[i]])
    mn.JohMer.ln[i,(t+1)] <- mu.JohMer.ln[year[i]] + rho.JohMer.ln[year[i]]*(JohMer.ln[i,t]-mu.JohMer.ln[year[i]])
  }
}

#MODEL PRIORS 
tau.total ~ dgamma(3.29,7.8)
K <- w.a + w.b + w.c + w.d + w.e + w.f
tau.model <- K*tau.total

w.a ~ dbern(0.5)
w.b ~ dbern(0.5)
w.c ~ dbern(0.5)
w.d ~ dbern(0.5)
w.e ~ dbern(0.5)
w.f ~ dbern(0.5)

#####PARAMETER PRIORS

#PARAMETER PRIORS - INSECT EFFECTS
b.Ann.ln ~ dnorm(0,tau.model)
b.Joh.ln ~ dnorm(0,tau.model)
b.Mer.ln ~ dnorm(0,tau.model)
b.AnnJoh.ln ~ dnorm(0,tau.model)
b.AnnMer.ln ~ dnorm(0,tau.model)
b.JohMer.ln ~ dnorm(0,tau.model)

#RANDOM EFFECTS AND ATTEMPT
for(i in 1:n.pair){
beta.P[i] ~ dnorm(int.pair,tau.pair)
}
int.pair ~ dnorm(0,0.001)
tau.pair <- pow(sigma.pair,-2)
sigma.pair ~ dunif(0,25) 

for(y in 1:n.year){
beta.Y[y] ~ dnorm(0,tau.year)
}
tau.year <- pow(sigma.year,-2)
sigma.year ~ dunif(0,25)

beta.S.attempt ~ dnorm(0,0.001)

#INSECT MODEL PARAMETERS 
for(y in 1:5){mu.Ann.ln[y] ~ dnorm(0,0.001)}
for(y in 1:5){tau.Ann.ln[y] ~ dgamma(0.1,0.1)}
for(y in 1:5){rho.Ann.ln[y] ~ dnorm(0,0.001)}  

for(y in 1:5){mu.Joh.ln[y] ~ dnorm(0,0.001)}
for(y in 1:5){tau.Joh.ln[y] ~ dgamma(0.1,0.1)}
for(y in 1:5){rho.Joh.ln[y] ~ dnorm(0,0.001)}  

for(y in 1:5){mu.Mer.ln[y] ~ dnorm(0,0.001)}
for(y in 1:5){tau.Mer.ln[y] ~ dgamma(0.1,0.1)}
for(y in 1:5){rho.Mer.ln[y] ~ dnorm(0,0.001)}  

for(y in 1:5){mu.AnnJoh.ln[y] ~ dnorm(0,0.001)}
for(y in 1:5){tau.AnnJoh.ln[y] ~ dgamma(0.1,0.1)}
for(y in 1:5){rho.AnnJoh.ln[y] ~ dnorm(0,0.001)}  

for(y in 1:5){mu.AnnMer.ln[y] ~ dnorm(0,0.001)}
for(y in 1:5){tau.AnnMer.ln[y] ~ dgamma(0.1,0.1)}
for(y in 1:5){rho.AnnMer.ln[y] ~ dnorm(0,0.001)}  

for(y in 1:5){mu.JohMer.ln[y] ~ dnorm(0,0.001)}
for(y in 1:5){tau.JohMer.ln[y] ~ dgamma(0.1,0.1)}
for(y in 1:5){rho.JohMer.ln[y] ~ dnorm(0,0.001)}  

}
